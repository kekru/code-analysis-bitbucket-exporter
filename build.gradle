import java.nio.file.Paths

plugins {
    id 'application'
    id "org.sonarqube" version "2.6.2"
    id 'org.gradle.java'
    id 'org.gradle.jacoco'
    id 'java'
    id 'pmd'
    id "com.github.spotbugs" version "4.2.0"
}

apply plugin: 'java'
apply plugin: 'checkstyle'

final File pmdXsd = Paths.get(projectDir.absolutePath, "src/main/resources/analysis/pmd/report_2_0_0.xsd").toFile()

// https://github.com/spotbugs/spotbugs/blob/4.0.3/spotbugs/etc/bugcollection.xsd
final File spotbugsXsd = Paths.get(projectDir.absolutePath, "src/main/resources/analysis/spotbugs/bugcollection.xsd").toFile()

repositories {
    jcenter()
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    //withJavadocJar()
    //withSourcesJar()
}

dependencies {
    implementation "org.slf4j:slf4j-api:1.7.25"
    implementation "org.slf4j:slf4j-simple:1.7.9"
    implementation "org.yaml:snakeyaml:1.26"
    implementation "com.cdancy:bitbucket-rest:2.6.2"
   /* implementation "com.atlassian.bitbucket.server:bitbucket-api:${bitbucketServerVersion}"
    implementation "com.atlassian.bitbucket.server:bitbucket-spi:${bitbucketServerVersion}"
    implementation "com.atlassian.bitbucket.server:bitbucket-git-api:${bitbucketServerVersion}"
    implementation "com.atlassian.bitbucket.server:bitbucket-build-api:${bitbucketServerVersion}"
    implementation "com.atlassian.bitbucket.server:bitbucket-branch-api:${bitbucketServerVersion}"
    implementation "com.atlassian.bitbucket.server:bitbucket-comment-likes-api:${bitbucketServerVersion}"
    implementation "com.atlassian.bitbucket.server:bitbucket-jira-api:${bitbucketServerVersion}"
    implementation "com.atlassian.bitbucket.server:bitbucket-ssh-api:${bitbucketServerVersion}"
    implementation "com.atlassian.bitbucket.server:bitbucket-scm-common:${bitbucketServerVersion}"
    implementation "com.atlassian.bitbucket.server:bitbucket-web-common:${bitbucketServerVersion}"*/
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.9'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-all:1.10.19'
    testImplementation 'org.hamcrest:hamcrest-all:1.3'


    implementation "org.projectlombok:lombok:1.18.4"
    annotationProcessor "org.projectlombok:lombok:1.18.4"
    testAnnotationProcessor "org.projectlombok:lombok:1.18.4"
}

clean {
    delete new java.util.HashSet([
        buildDir,
        "src/generated"
    ])
}

task generateXmlClasses {
    inputs.files pmdXsd, spotbugsXsd
    outputs.dir "src/generated/java/de/kekru/codeanalysisbb"
    doLast {
        "xjc -encoding UTF-8 -d src/generated/java -p de.kekru.codeanalysisbb.generated.pmd ${pmdXsd.absolutePath}"
            .execute()
            .waitForProcessOutput(System.out, System.err)

        "xjc -encoding UTF-8 -d src/generated/java -p de.kekru.codeanalysisbb.generated.spotbugs ${spotbugsXsd.absolutePath}"
                .execute()
                .waitForProcessOutput(System.out, System.err)
    }
}

compileJava.dependsOn(generateXmlClasses)

sourceSets {
    generated {
        java {
            srcDirs 'src/generated/java'
        }
    }
    main {
        compileClasspath += sourceSets.generated.runtimeClasspath
        runtimeClasspath += sourceSets.generated.runtimeClasspath
        java {
            srcDirs 'src/main/java'
        }
    }
    test {
        compileClasspath += sourceSets.generated.runtimeClasspath
        runtimeClasspath += sourceSets.generated.runtimeClasspath
        java {
            srcDirs 'src/test/java'
        }
    }
}

jar {
    from sourceSets.generated.output
    from sourceSets.main.output
}

application {
    mainClassName = 'de.kekru.codeanalysisbb.Main'
}


sonarqube {
    properties {
        property 'sonar.jacoco.reportPaths', 'build/jacoco/test.exec'
        property 'sonar.junit.reportPaths', 'build/test-results/test'

        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.host.url", "https://sonarqube.example.com"
        property "sonar.login", ""
        property "sonar.analysis.mode", "preview"
        property "sonar.issuesReport.html.enable", "true"
        property "sonar.projectKey", "code-analysis-bb"
    }
}


// PMD
pmd {
    // Configuration see: https://docs.gradle.org/current/dsl/org.gradle.api.plugins.quality.PmdExtension.html
    consoleOutput = true
    toolVersion = "6.21.0"
    rulePriority = 5
    ruleSets = ["category/java/errorprone.xml", "category/java/bestpractices.xml"]
    ignoreFailures = true
    sourceSets = [sourceSets.main, sourceSets.test]
}


// Checkstyle
// https://medium.com/@raveensr/how-to-add-checkstyle-and-findbugs-plugins-in-a-gradle-based-project-51759aa843be
checkstyle {
    toolVersion '7.8.1'
    ignoreFailures = true
    configFile file("config/checkstyle/checkstyle.xml")
}
checkstyleMain {
    source ='src/main/java'
}
checkstyleTest {
    source ='src/test/java'
}
tasks.withType(Checkstyle) {
    reports {
        xml.enabled true
        html.enabled true
    }
}


// Spotbugs
// https://github.com/spotbugs/spotbugs-gradle-plugin#readme
spotbugs {
    toolVersion = '4.0.3'
    ignoreFailures = true
}
